import os

# To run locally:
# source activate snakemake
# snakemake --keep-going --cores 4 --use-singularity --singularity-prefix ~/.singularity --singularity-args "--home ~"

# To run on tars:
# module load conda && source /local/gensoft2/exe/conda/3.19.0/conda/bin/activate snakemake && module load singularity
# snakemake --keep-going --cores 1 --use-singularity --singularity-prefix ~/.singularity --singularity-args "--home ~" --cluster "sbatch -c {threads} -o logs/{params.name}.log -e logs/{params.name}.log --mem {params.mem} -A bioevo -p bioevo,dedicated,common --qos={params.qos} -J {params.name}" --jobs 300
# on bioevo:
# snakemake --keep-going --cores 1 --use-singularity --singularity-prefix ~/.singularity --singularity-args "--home ~" --cluster "sbatch -c {threads} -o logs/{params.name}.log -e logs/{params.name}.log --mem {params.mem} -p bioevo --qos=bioevo -A bioevo -J {params.name}" --jobs 300

# To visualise the pipeline:
# module load graphviz
# snakemake --dag | dot -Tsvg > acr_dag.svg


configfile: "config.yaml"
localrules: all
ruleorder: pastml_drm_loc_subtree > timed_pastml_drm > pastml_drm_loc > pastml_loc > pastml_drm


folder = os.path.abspath(config["folder"])
data_dir = os.path.join(folder, config['data_dir'])
metadata = os.path.join(data_dir, 'metadata.tab')
metadata_loc = os.path.join(data_dir, 'metadata_loc.tab')

# The folder where the reconstructed ACR maps will be placed
maps_dir = 'maps'

# Number of different tree topologies to be analysed
n=5

# Number of resampled trees to be analysed
r=5

# Max number of tips per Location to keep in each resampled tree
s=250

# SDRMs to be analysed
DRMS = ['RT:M184V', 'RT:K103N', 'RT:D67N', 'RT:K70R', 'RT:Y181C']

# choose one or more of the tree types below
tree_types = [ 'raxml', 'phyml', 'fast']

location_col = 'Loc'

model='F81'

# Years to be analysed (for each year a tree will be produced containing only the tips sampled not-after this year)
# last is the latest year in the dataset, first is the first year containing a resistance mutation of interest,
# mid is the last year - 10.
YEARS = ['last', 'mid', 'first']

rule all:
    input:
        drm_prevalence = os.path.join(data_dir, 'prevalence.tab'),

        geo_map = expand(os.path.join(data_dir, maps_dir, '{type}', 'Loc_{loc}', 'geo_map_best_{type}_{loc}_tree.html'), \
        type=tree_types, loc=location_col),

        loc_maps = expand(os.path.join(data_dir, maps_dir, '{type}', 'Loc_{loc}', 'threshold_{t}', \
        'map_{n}.tree_{type}.model_{model}.location_{loc}.threshold_{t}.tree.html'), \
        n=['best'], type=tree_types, model='F81', loc=location_col, t=[15]),

#        sampled_loc_maps = expand(os.path.join(data_dir, maps_dir, '{type}', 'Loc_{loc}', 'threshold_{t}', \
#        'map_best.tree_{type}.model_{model}.location_{loc}.threshold_{t}.tree.sampled_{s}.state_{loc}.rep_{r}.html'), \
#        type=tree_types, model=model, loc=location_col, r=range(r), s=s, t=5),
#
#        drm_maps = expand(os.path.join(data_dir, maps_dir, '{type}', 'DRM_{DRM}', 'pastml_{type}_{n}_{model}_tree.html'), \
#        type=tree_types, DRM=DRMS, model=model, n=list(range(n)) + ['best']),
#
#        drm_loc_maps = expand(os.path.join(data_dir, maps_dir, '{type}', 'DRM_{DRM}', 'Loc_{loc}', \
#        'pastml_{type}_best_{model}_tree.html'), type=tree_types, DRM=DRMS, model=model, loc=location_col),
#
#        drm_times_maps = expand(os.path.join(data_dir, maps_dir, '{type}', 'DRM_{DRM}', 'best_timed', \
#        'pastml_{type}_{model}_tree.year_{r}.html'), r=YEARS, model=model, type=tree_types, DRM=DRMS),
#
#        TDR_maps = expand(os.path.join(data_dir, maps_dir, '{type}', 'DRM_{DRM}', 'Loc_{loc1}', 'pastml_{type}_best_{model}_subtree.TDR_loc_{loc}.html'), \
#        model=model, type=tree_types, DRM=DRMS, loc=location_col, loc1='Country')

rule loc_metadata:
    '''
    Adds Loc column to metadata
    '''
    input:
        data = metadata
    output:
        data = metadata_loc
    params:
        mem = 2000,
        name = 'loc_md'
    threads: 1
    singularity: "docker://evolbioinfo/python-evol:v3.6"
    shell: "python3 {folder}/py/map_location.py --output_data {output.data} --input_data {input.data}"

rule timed_trees:
    '''
    Prunes a tree for a collections of years.
    '''
    input:
        tree = os.path.join(data_dir, '{n}', 'pastml_{type}_tree.nwk'),
        data = metadata_loc
    output:
        trees = expand(os.path.join(data_dir, '{{n}}', 'pastml_{{type}}_tree_drm_{{drm}}.year_{r}.nwk'), r=YEARS)
    threads: 1
    params:
        mem = 1000,
        name = 'time_tree',
        tree = os.path.join(data_dir, '{n}', 'pastml_{type}_tree_drm_{drm}.year_{}.nwk'),
        drm = '{drm}',
        date_col = 'Year',
    singularity: "docker://evolbioinfo/pastml:v1.9.1"
    shell:
        """
        python3 {folder}/py/timeline_tree.py --in_tree {input.tree} --metadata {input.data} \
        --out_tree_pattern {params.tree} --drm {params.drm} --date_column {params.date_col}
        """

rule timed_pastml_drm:
    '''
    Reconstructs tree ancestral states with PASTML and visualises the result for a collections of years.
    '''
    input:
        trees = expand(os.path.join(data_dir, '{{n}}', 'pastml_{{type}}_tree_drm_{{drm}}.year_{r}.nwk'), r=YEARS),
        data = metadata_loc,
        pars = os.path.join(data_dir, 'pastml', 'params.model_{model}.tree_{n}.type_{type}.drm_{drm}.csv')
    output:
        maps = expand(os.path.join(data_dir, maps_dir, '{{type}}', 'DRM_{{drm}}', '{{n}}_timed', 'pastml_{{type}}_{{model}}_tree.year_{r}.html'), r=YEARS)
    threads: 1
    params:
        mem = 1000,
        name = 'timed_map_{drm}',
        drm = '{drm}',
        model = '{model}',
        date_col = 'Year',
    singularity: "docker://evolbioinfo/pastml:v1.9.1"
    shell:
        """
        python3 {folder}/py/acr.py --trees {input.trees} --data {input.data} --htmls {output.maps} \
        --columns {params.drm} --model {params.model} --date_column {params.date_col} \
        --parameters {input.pars}
        """

rule pastml_drm:
    '''
    Reconstructs tree ancestral states with PASTML and visualises the result.
    '''
    input:
        tree = os.path.join(data_dir, '{n}', 'pastml_{type}_tree.nwk'),
        data = metadata,
    output:
        map = os.path.join(data_dir, maps_dir, '{type}', 'DRM_{DRM}', 'pastml_{type}_{n}_{model}_tree.html'),
        pars = os.path.join(data_dir, 'pastml', 'params.model_{model}.tree_{n}.type_{type}.drm_{DRM}.csv')
    threads: 1
    params:
        mem = 4000,
        name = 'p_{n}_{DRM}',
        model = '{model}',
        date_col = 'Year',
        drm = '{DRM}',
        work_dir = os.path.join(data_dir, 'pastml', 'drm_{DRM}', 'tree_{type}_{n}')
    singularity: "docker://evolbioinfo/pastml:v1.9.1"
    shell:
        """
         python3 {folder}/py/acr.py --trees {input.tree} --data {input.data} --htmls {output.map} \
        --columns {params.drm} --model {params.model} --date_column {params.date_col} \
        --out_parameters {output.pars} --work_dir {params.work_dir}
        """

rule pastml_drm_loc:
    '''
    Reconstructs tree ancestral states with PASTML and visualises the result.
    '''
    input:
        tree = os.path.join(data_dir, '{n}', 'pastml_{type}_tree.nwk'),
        data = metadata_loc,
        loc_pars = os.path.join(data_dir, 'pastml', 'params.model_{model}.tree_{n}.type_{type}.loc_{loc}.threshold_5.tree.csv'),
        drm_pars = os.path.join(data_dir, 'pastml', 'params.model_{model}.tree_{n}.type_{type}.drm_{DRM}.csv'),
    output:
        map = os.path.join(data_dir, maps_dir, '{type}', 'DRM_{DRM}', 'Loc_{loc}', 'pastml_{type}_{n}_{model}_tree.html'),
        data = os.path.join(data_dir, 'pastml', 'pastml_{type}_{n}_{model}_tree_DRM_{DRM}_Loc_{loc}.tab'),
    threads: 2
    params:
        mem = 4000,
        name = 'pastml_{n}',
        model = '{model}',
        date_col = 'Year',
        drm = '{DRM}',
        loc = '{loc}'
    singularity: "docker://evolbioinfo/pastml:v1.9.1"
    shell:
        """
        python3 {folder}/py/acr.py --trees {input.tree} --data {input.data} --htmls {output.map} \
        --columns {params.loc} {params.drm} --name_column {params.loc} --date_col {params.date_col} \
        --model {params.model} --parameters {input.loc_pars} {input.drm_pars} --out_data {output.data}
        """

rule pastml_drm_loc_subtree:
    '''
    Reconstructs tree ancestral states with PASTML and visualises the result.
    '''
    input:
        tree = os.path.join(data_dir, '{n}', 'pastml_{type}_tree.nwk'),
        states = os.path.join(data_dir, 'pastml', 'pastml_{type}_{n}_{model}_tree_DRM_{DRM}_Loc_{loc}.tab'),
        data = metadata_loc,
        drm_pars = os.path.join(data_dir, 'pastml', 'params.model_{model}.tree_{n}.type_{type}.drm_{DRM}.csv'),
    output:
        map = os.path.join(data_dir, maps_dir, '{type}', 'DRM_{DRM}', 'Loc_{loc1}', 'pastml_{type}_{n}_{model}_subtree.TDR_loc_{loc}.html'),
        subtree = temp(os.path.join(data_dir, '{n}', 'pastml_{type}_subtree.drm_{DRM}.loc_{loc}.loc1_{loc1}.model_{model}.nwk'))
    threads: 1
    params:
        mem = 4000,
        name = 'p_{n}_{DRM}_{loc1}',
        model = '{model}',
        date_col = 'Year',
        drm = '{DRM}',
        loc = '{loc}',
        loc1 = '{loc1}'
    singularity: "docker://evolbioinfo/pastml:v1.9.1"
    shell:
        """
        python3 {folder}/py/find_TDR_root.py --tree {input.tree} --states {input.states} --drm {params.drm} \
        --loc {params.loc} --out_tree {output.subtree}

        python3 {folder}/py/acr.py --trees {output.subtree} --data {input.data} --htmls {output.map} \
        --columns {params.drm} {params.loc1} --name_column {params.loc1} --date_column {params.date_col} \
        --model {params.model} --parameters {input.drm_pars} --tip_size_threshold 12
        """

rule pastml_loc:
    '''
    Reconstructs tree ancestral states with PASTML and visualises the result.
    '''
    input:
        tree = os.path.join(data_dir, '{n}', 'pastml_{type}_{suffix}.nwk'),
        data = metadata_loc
    output:
        map = os.path.join(data_dir, maps_dir, '{type}', 'Loc_{loc}', 'threshold_{t}', 'map_{n}.tree_{type}.model_{model}.location_{loc}.threshold_{t}.{suffix}.html'),
        pars = os.path.join(data_dir, 'pastml', 'params.model_{model}.tree_{n}.type_{type}.loc_{loc}.threshold_{t}.{suffix}.csv'),
    threads: 1
    params:
        mem = 4000,
        name = 'pastml_{n}',
        loc = '{loc}',
        model = '{model}',
        date_col = 'Year',
        threshold = '{t}',
        # if pars does not exist it will be just ignored, otherwise we won't redo a long PastML task
        # just to trim a bit less in the visualisation
        pars = '--parameters {}'.format(os.path.join(data_dir, 'pastml', 'params.model_{model}.tree_{n}.type_{type}.loc_{loc}.threshold_5.{suffix}.csv'))
        if os.path.isfile(os.path.join(data_dir, 'pastml', 'params.model_{model}.tree_{n}.type_{type}.loc_{loc}.threshold_5.{suffix}.csv')) else ''
    singularity: "docker://evolbioinfo/pastml:v1.9.1"
    shell:
        """
        python3 {folder}/py/acr.py --trees {input.tree} --data {input.data} --htmls {output.map} \
        --columns {params.loc} --date_column {params.date_col} \
        --model {params.model} --tip_size_threshold {params.threshold} \
        --out_parameters {output.pars} {params.pars} --verbose
        """

rule geomap:
    '''
    Reconstructs the geographical map coloured according to PastML categories.
    '''
    input:
        tree = os.path.join(data_dir, '{n}', 'pastml_{type}_{suffix}.nwk'),
        data = metadata_loc
    output:
        geo_map = os.path.join(data_dir, maps_dir, '{type}', 'Loc_{loc}', 'geo_map_{n}_{type}_{loc}_{suffix}.html'),
    threads: 1
    params:
        mem = 4000,
        name = 'pastml_{n}',
        loc = '{loc}',
        country_col = 'Country'
    singularity: "docker://evolbioinfo/pastml:v1.9.1"
    shell:
        """
        python3 {folder}/py/get_geomap.py --tree {input.tree} --metadata {input.data} \
        --geo_html {output.geo_map} --location_col {params.loc} --country_col {params.country_col}
        """

rule sample_tree:
    '''
    Samples the tree to keep at most s tips per each state.
    '''
    input:
        tree = os.path.join(data_dir, '{n}', 'pastml_{type}_tree.nwk'),
        data = metadata_loc,
    output:
        tree = os.path.join(data_dir, '{n}', 'pastml_{type}_tree.sampled_{s}.state_{loc}.rep_{r}.nwk')
    threads: 1
    params:
        mem = 1000,
        name = 'sample',
        s = '{s}',
        col = '{loc}'
    singularity: "docker://evolbioinfo/python-evol:v3.6"
    shell:
        """
        python3 {folder}/py/sample_tree.py --in_tree {input.tree} --metadata {input.data} --out_tree {output.tree} \
        --n {params.s} --column {params.col}
        """

rule prevalence:
    '''
    Selects n most common SDRMs.
    '''
    input:
        tab = metadata
    output:
        tab = os.path.join(data_dir, 'prevalence.tab')
    threads: 1
    params:
        mem = 1000,
        name = 'prev'
    singularity: "docker://evolbioinfo/python-evol:v3.6"
    shell: "python3 {folder}/py/prevalence.py --input {input.tab} --output {output.tab}"
