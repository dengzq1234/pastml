import os

# To run locally:
# snakemake --keep-going --cores 4

configfile: "config.yaml"
localrules: all

src_command = config['src']
py_config = config['py_config']

folder = os.path.abspath(config["folder"])
data_dir = os.path.join(folder, config['data_dir'])

a, c, g, t = .6, .15, .2, .05

trees = ['tree.152taxa', 'tree']
#trees = ['tree.152taxa']
sfs = [0.01, 0.05, .1, 0.5, 1, 2]
#sfs = [.5]


rule all:
    input:
        expand(os.path.join(folder, 'html', '{tree}.sf_{SF}.A_{A}.C_{C}.G_{G}.T_{T}.tree.html'), SF=sfs,
         A=a, C=c, G=g, T=t, tree=trees),
        expand(os.path.join(folder, 'html', '{tree}.sf_{SF}.A_{A}.C_{C}.G_{G}.T_{T}.tree.html'), SF=sfs,
         A=.25, C=.25, G=.25, T=.25, tree=trees),
        expand(os.path.join(folder, 'html', '{tree}.sf_{SF}.JTT.tree.html'), SF=sfs, tree=trees),


rule convert_tree:
    '''
    Convert a tree to another format.
    '''
    input:
        tree = '{tree}.nwk'
    output:
        tree = '{tree}.seqgen.nwk',
    params:
        mem = 1000,
        name = 'prepare_tree'
    threads: 1
    run:
        shell("""
            {src_command}
            {py_config}

            python3 {folder}/py/prepare_tree.py --output {output.tree} --input {input.tree}
        """)

rule generate_seq:
    '''
    Generates sequences.
    '''
    input:
        tree = os.path.join(data_dir, '{tree}.seqgen.nwk')
    output:
        log = temp(os.path.join(folder, 'pastml', '{tree}.sf_{SF}.A_{A}.C_{C}.G_{G}.T_{T}.tab')),
        tab = os.path.join(folder, 'pastml', '{tree}.sf_{SF}.A_{A}.C_{C}.G_{G}.T_{T}.pastml.tab'),
        tree = os.path.join(folder, 'pastml', '{tree}.sf_{SF}.A_{A}.C_{C}.G_{G}.T_{T}.nwk'),
        html = os.path.join(folder, 'html', '{tree}.sf_{SF}.A_{A}.C_{C}.G_{G}.T_{T}.tree.html'),
        map = os.path.join(folder, 'html', '{tree}.sf_{SF}.A_{A}.C_{C}.G_{G}.T_{T}.map.html'),
        html_hky = os.path.join(folder, 'html', '{tree}.sf_{SF}.A_{A}.C_{C}.G_{G}.T_{T}.tree_HKY.html'),
        map_hky = os.path.join(folder, 'html', '{tree}.sf_{SF}.A_{A}.C_{C}.G_{G}.T_{T}.map_HKY.html'),
    params:
        mem = 1000,
        name = 'seqgen',
        seqgen = config['seq-gen'],
        sf = '{SF}',
        a = '{A}',
        c = '{C}',
        g = '{G}',
        t = '{T}',
        wd = os.path.join(folder, 'pastml', '{tree}', 'sf_{SF}.A_{A}.C_{C}.G_{G}.T_{T}'),
        wd_hky = os.path.join(folder, 'pastml', '{tree}', 'sf_{SF}.A_{A}.C_{C}.G_{G}.T_{T}_HKY')
    threads: 4
    run:
        shell("""
            {src_command}
            {py_config}

            {params.seqgen} -m F84 -l 1 -s {params.sf} -f {params.a} {params.c} {params.g} {params.t} \
            -z 239 -wa -op {input.tree} > {output.log}


            python3 {folder}/py/prepare_annotation.py --output_tab {output.tab} --output_tree {output.tree} \
            --input_tree {input.tree} --input_log {output.log}

            pastml -t {output.tree} -d {output.tab} --html {output.html} -p {output.map} --work_dir {params.wd} -v \
            --prediction_method COPY ML --no_forced_joint

            pastml -t {output.tree} -d {output.tab} --html {output.html_hky} -p {output.map_hky} --work_dir {params.wd_hky} -v \
            --prediction_method COPY ML --model HKY --no_forced_joint
        """)

rule generate_seq_jtt:
    '''
    Generates sequences.
    '''
    input:
        tree = os.path.join(data_dir, '{tree}.seqgen.nwk')
    output:
        log = temp(os.path.join(folder, 'pastml', '{tree}.sf_{SF}.JTT.tab')),
        tab = os.path.join(folder, 'pastml', '{tree}.sf_{SF}.JTT.pastml.tab'),
        tree = os.path.join(folder, 'pastml', '{tree}.sf_{SF}.JTT.nwk'),
        html = os.path.join(folder, 'html', '{tree}.sf_{SF}.JTT.tree.html'),
        map = os.path.join(folder, 'html', '{tree}.sf_{SF}.JTT.map.html'),
        html_JTT = os.path.join(folder, 'html', '{tree}.sf_{SF}.JTT.tree_JTT.html'),
        map_JTT = os.path.join(folder, 'html', '{tree}.sf_{SF}.JTT.map_JTT.html'),
    params:
        mem = 1000,
        name = 'seqgen',
        seqgen = config['seq-gen'],
        sf = '{SF}',
        wd = os.path.join(folder, 'pastml', '{tree}', 'sf_{SF}.JTT'),
        wd_JTT = os.path.join(folder, 'pastml', '{tree}', 'sf_{SF}.JTT_JTT')
    threads: 4
    run:
        shell("""
            {src_command}
            {py_config}

            {params.seqgen} -m JTT -l 1 -s {params.sf} -z 239 -wa -op {input.tree} > {output.log}


            python3 {folder}/py/prepare_annotation.py --output_tab {output.tab} --output_tree {output.tree} \
            --input_tree {input.tree} --input_log {output.log}

            pastml -t {output.tree} -d {output.tab} --html {output.html_JTT} -p {output.map_JTT} --work_dir {params.wd_JTT} -v \
            --prediction_method COPY ML --model JTT --no_forced_joint

            pastml -t {output.tree} -d {output.tab} --html {output.html} -p {output.map} --work_dir {params.wd} -v \
            --prediction_method COPY ML --no_forced_joint
        """)
